#@TYPE: Machine
#@NAME: fsref-zynq7
#@DESCRIPTION: copy frm picozed-zynq7.conf

################################################################################
# require conf/machine/include/tune-zynq.inc
DEFAULTTUNE ?= "cortexa9thf-neon"
SOC_FAMILY ?= "zynq"

# Available SOC_VARIANT's for zynq:
# 7zs - Zynq-7000 Single A9 Core
# 7z  - Zynq-7000 Dual A9 Core
SOC_VARIANT ?= "7z"

require conf/machine/include/tune-cortexa9.inc
require conf/machine/include/soc-family.inc


################################################################################
#require conf/machine/include/machine-xilinx-overrides.inc
MACHINEOVERRIDES =. "${@['', '${SOC_FAMILY}${SOC_VARIANT}:']['${SOC_VARIANT}' != '']}"

PACKAGE_EXTRA_ARCHS_append = " ${SOC_FAMILY}"
PACKAGE_EXTRA_ARCHS_append = "${@['', ' ${SOC_FAMILY}${SOC_VARIANT}']['${SOC_VARIANT}' != '']}"


################################################################################
#require conf/machine/include/machine-xilinx-default.inc
# Default Xilinx BSP Machine settings

MACHINE_FEATURES_BACKFILL_CONSIDERED += "rtc"

# File System Configuration
IMAGE_FSTYPES ?= "tar.gz cpio cpio.gz.u-boot"

XSERVER ?= " \
	xserver-xorg \
	xf86-input-evdev \
	xf86-input-mouse \
	xf86-input-keyboard \
	xf86-video-fbdev \
	"
FPGA_MNGR_RECONFIG_ENABLE ?= "${@bb.utils.contains('IMAGE_FEATURES', 'fpga-manager', '1', '0', d)}"

################################################################################
MACHINE_FEATURES = ""

# u-boot configuration
PREFERRED_PROVIDER_virtual/bootloader = "u-boot"
UBOOT_MACHINE = "zynq_fsref_defconfig"
SPL_BINARY ?= "spl/boot.bin"
UBOOT_ELF = "u-boot"
UBOOT_SUFFIX ?= "img"
UBOOT_BINARY ?= "u-boot.${UBOOT_SUFFIX}"

# kernel configuration
PREFERRED_PROVIDER_virtual/kernel = "linux"
KERNEL_IMAGETYPE = "uImage"
KERNEL_IMAGETYPES += "zImage"
# NOTE: when loaded by u-boot SPL, it should skip spl bss/pool,
#       please see zynq-common.h in uboot
#         CONFIG_SYS_SPL_MALLOC_START	// in config is 2MB
#         CONFIG_SYS_SPL_MALLOC_SIZE	// in zynq-common.h is 32MB
#         CONFIG_SPL_BSS_START_ADDR	// 1MB
#         CONFIG_SPL_BSS_MAX_SIZE	// 1MB
# NOTE: lower 1 MB of DDR may be not valid at SPL stage.
# NOTE: the u-boot will located at 64MB (CONFIG_SYS_TEXT_BASE in config)
# NOTE: https://forums.xilinx.com/t5/Embedded-Linux/How-to-boot-linux-by-bypassing-uboot/td-p/526823
#      1. Alter the Linux kernel configuration to use an appended DTB.
#      2. Build the kernel (zImage) and the device tree.
#      3. this assumes using a initramfs ramdisk as Petalinux does to get a single monolithic binary image
#      4. Concatenate the kernel and dtb files into one file (cat zImage system.dtb > zImage.withdtb.bin)
#      5. Create a BIF file to only load FSBL and zImage.withdtb.bin.
#      6. image : {
#      7.         [bootloader]zynq_fsbl.elf
#      8.         [load=0x8800000,startup=0x8800000]zImage.withdtb.bin
#      9. }
#     10. Build a new boot.bin
# Note: Linux must be loaded at or above 0x8800000 to prevent the Linux kernel
#       wrapper from decompressing the kernel into the lower 1 MB of DDR which
#       is not mapped into low memory at this point as itâ€™s typically done in
#       u-boot. It appears that Linux does map the lower 1 MB DDR into low
#       memory as devmem works for address 0 but this probably needs some verifying.
KERNEL_EXTRA_ARGS_fsref += "UIMAGE_LOADADDR=0x8800000"
KERNEL_MACHINE = "fsref_defconfig"
KERNEL_DEVICETREE = "zynq-fsref.dtb"

# NOTE: drop virtual/boot-bin, because u-boot doesn't provide it.
EXTRA_IMAGEDEPENDS += " \
		virtual/bootloader \
		"

SERIAL_CONSOLE = "115200 ttyPS0"

IMAGE_BOOT_FILES = " \
		boot.bin \
		${UBOOT_BINARY} \
		${KERNEL_IMAGETYPE} \
		${KERNEL_IMAGETYPES} \
		${@os.path.basename(KERNEL_DEVICETREE)} \
		"

PACKAGECONFIG_pn-opencv := ""
# NOTE: remove python dependency for gdb (meta-qt5)
PACKAGECONFIG_remove_pn-gdb = "python"
